; $Id: read_monortm.pro,v 1.1 2006/01/26 16:23:26 dturner Exp $
;+ 
; Abstract:
;	This script reads in the ascii output generated by monortm.
;
; Author:
;	Dave Turner
;		SSEC / University of Wisconsin - Madison
;
; Date:
;	Jan 2006
;
; Output:
;    The function returns the following structure
;	filename 	The name of the file read in
;	nwn 		The number of frequencies computed
;	freq 		The frequencies of the calculations [GHz]
;	tb 		The computed brightness temperatures [K]
;	trans		The computed transmission of the atmosphere 
;	tau		The computed column optical depth due to all species
;	pwv		The precipitable water vapor used in the calculation [cm]
;	lwp		The liquid water path used in the calculation [g/m2]
;
; Call:
  function read_monortm, $	; Returns a structure (defined above)
  	filename, $		; The name of the file to read
	dostop=dostop		; Set this keyword to stop inside the routine
;-

  files = findfile(filename, count=count)
  if(count ne 1) then begin
    print,'Error: Unable to uniquely determine ' + filename
    return,0
  endif

  openr,lun,files(0),/get_lun
  header = replicate(' ',3)
  readf,lun,header
  parts = str_sep(header(2),':')
  nwn = fix(parts(1))
  data = fltarr(23,nwn)
  readf,lun,data
  free_lun,lun

  if(keyword_set(dostop)) then stop,'Stopped inside routine'
  return,{filename:files(0),nwn:nwn,freq:reform(data(3,*)),tb:reform(data(4,*)), $
  	trans:reform(data(6,*)),tau:reform(data(13,*)),pwv:data(7,0),lwp:data(8,0)*1000.}
end
