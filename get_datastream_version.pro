; $Id: get_datastream_version.pro,v 1.4 2002/02/07 16:18:03 dturner Release_ddt_1_13 $
;+
; Abstract:
;	This routine is designed to return the version of the software that
;    created this datastream, whether it was generated by the SGP Site Data
;    System or the Experiment Center.
;
; Author:
;	Dave Turner, PNNL
;
; Date:
;	November 1999
;
; Call:
	function get_datastream_version, Cdfid
;-

  version = '-999'
  version_found_but_not_defined = '-888'

  inqresults = ncdf_inquire(Cdfid)
  ngatts = inqresults.ngatts
  for i=0, ngatts -1 do begin
    result = ncdf_attname(Cdfid, /global, i)

    		; Check for SGP SDS attribute first
    if(result eq 'ingest-software') then begin
      ncdf_attget, Cdfid, /global, 'ingest-software', value
			; Found it so we now need to decompose the version number
      parts = str_sep(string(value), ' ')

      			; Jim Liljegren reprocessed the MWR data and did not
			;   put spaces everywhere that the SDS does...
      if(parts(n_elements(parts)-1) eq 'Liljegren') then version = parts(1) $
      else version = parts(2)
      return, version
    endif

    		; Check for EC/VAP attribute next
    if(result eq 'Version') then begin
      ncdf_attget, Cdfid, /global, 'Version', value
			; Found it so we now need to decompose the version number
      value = string(value)
      pos = strpos(value, 'Release')
      if(pos lt 0) then return, version_found_but_not_defined $
      else if(pos gt 0) then begin
        version = '*'
	value = strmid(value, pos, strlen(value))
      endif else version = ''
      parts = str_sep(value, ' ')
      value = parts(0)
      parts = str_sep(value, '_')
      if(parts(1) eq 'ddt') then version = version + parts(2) + '.' + parts(3) $
      else version = version + parts(1) + '.' + parts(2)
      return, version
    endif

  endfor

  return, version
end
